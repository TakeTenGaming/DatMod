buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}

	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

plugins { id "com.matthewprenger.cursegradle" version "1.0.10" }

apply plugin: "net.minecraftforge.gradle.forge"

version = "${build_major}.${build_minor}.${build_revision}"
group = "net.taketengaming.datmod"
archivesBaseName = "DatMod"

// This is needed so Eclipse tasks generate correctly
sourceCompatibility = targetCompatibility = "${java_compatibility_version}"
compileJava {
	sourceCompatibility = targetCompatibility = "${java_compatibility_version}"
}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	mappings = "snapshot_20171003"
	//makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
	maven { // Just Enough Items
		name = "Progwml6 maven"
		url = "http://dvs1.progwml6.com/files/maven"
	}

	maven { // Just Enough Items fallback
		name = "ModMaven"
		url = "modmaven.k-4u.nl"
	}

	maven { // Redstone Flux
		name = "CoFH Maven"
		url = "http://maven.covers1624.net/"
	}

	maven { // TOP
		name = "tterrag maven"
		url = "http://maven.tterrag.com/"
	}
}

dependencies {
	// JustEnoughItems
	deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}:api" // Compile against the API
	runtime "mezz.jei:jei_${mc_version}:${jei_version}" // Use the full jar at runtime

	// Redstone Flux
	deobfCompile "cofh:RedstoneFlux:${redstoneFlux_version}:universal"

	// The One Probe
	deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:${theOneProbe_version}"
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"

		// replace version and mcversion
		expand "version": project.version, "mcversion": project.minecraft.version
	}

	// copy everything else except the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

curseforge {
	Properties props = new Properties()
	props.load(new FileInputStream("$project.rootDir/gradle-local.properties"))
	props.each { prop ->
		project.ext.set(prop.key, prop.value)
	}

	apiKey = "${curseForge_apiKey}"

	project {
		id = "${curse_projectId}"
		changelog = file("CHANGELOG.md")
		changelogType = "markdown"
		releaseType = "release"

		mainArtifact(jar) {
			displayName = "DatMod-${mc_version}-${version}"
		}
	}
}