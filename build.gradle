buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}

	dependencies {
		// ForgeGradle
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = mc_version + "-" + mod_version
group = "taketengaming.datmod"
archivesBaseName = "DatMod"

// Eclipse task compat
sourceCompatibility = targetCompatibility = java_compatibility
compileJava {
	sourceCompatibility = targetCompatibility = java_compatibility
}

minecraft {
	version = mc_version + "-" + forge_version
	runDir = "run"
	makeObfSourceJar = true
	useDepAts = true
	mappings = mcp_mappings
}

task deobfJar(type: Jar) { // Generate deobfuscated
	from sourceSets.main.output
	classifier = 'deobf'
}
tasks.build.dependsOn('deobfJar')
tasks.build.dependsOn('generatePomFileForMavenJavaPublication')
tasks.build.dependsOn('publishToMavenLocal')

repositories {
	// Local Maven
	mavenLocal()

	// JustEnoughItems
	maven { url = "http://dvs1.progwml6.com/files/maven" }

	// HWYLA
	maven { url = "http://tehnut.info/maven" }
}

dependencies {
	// TenCore
	deobfCompile "taketengaming.tencore:TenCore:" + tencore_version

	// JustEnoughItems
	deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}:api"
	runtime "mezz.jei:jei_${mc_version}:${jei_version}"

	// HWYLA
	deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourceJar {
				classifier "sources"
			}
		}
	}
}